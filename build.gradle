// BunyipsLib Gradle configuration

import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'com.android.library' apply false
    id 'org.jetbrains.dokka'
}

//dependencies {
//    dokkaHtmlPlugin 'org.jetbrains.dokka:kotlin-as-java-plugin:1.9.20'
//}

def getCommit = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

def getBuildTime = { ->
    return new Date().format("yyyy-MM-dd@HH:mm:ss", TimeZone.getDefault())
}

def getRandomID = { ->
    return UUID.randomUUID().toString()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: "../build.dependencies.gradle"

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 24
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28
        buildConfigField "String", "GIT_COMMIT", '"' + getCommit() + '"'
        buildConfigField "String", "BUILD_TIME", '"' + getBuildTime() + '"'
        buildConfigField "String", "ID", '"' + getRandomID() + '"'
        buildConfigField "String", "SEMVER", '"' + project.version + '"'
        buildConfigField "String", "SDK_VER", '"' + project.sdk_version + '"'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace = 'org.murraybridgebunyips.bunyipslib'
}

tasks.dokkaHtml.configure {
    suppressInheritedMembers.set(true)
    outputDirectory.set(file("./docs"))
    moduleVersion.set(project.version + "-dev-" + getBuildTime())

    dokkaSourceSets {
        configureEach {
            jdkVersion.set(8)
            includeNonPublic.set(true)
            perPackageOption {
                matchingRegex.set("org\\.murraybridgebunyips\\.bunyipslib\\.example\\..*")
                suppress.set(true)
            }
        }
    }
}

tasks.withType(DokkaTask.class).configureEach {
    String dokkaBaseConfiguration = """
    {
      "footerMessage": "Â© 2024 Lucas Bubner, Murray Bridge High School Student Robotics Club"
    }
    """
    pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": dokkaBaseConfiguration])
}

