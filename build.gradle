// BunyipsLib Gradle configuration

import org.jetbrains.dokka.DokkaConfiguration
import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'com.android.library' apply false
    id 'org.jetbrains.dokka'
}

// Kotlin-as-Java exposes Kotlin public fields strangely and makes it misleading
// so we'll choose not to use this plugin although it would be preferred. Better to keep things
// accurate and consistent.
//dependencies {
//    dokkaHtmlPlugin 'org.jetbrains.dokka:kotlin-as-java-plugin:1.9.20'
//}

def getCommit = { boolean suppressDirty = false ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            if (suppressDirty) {
                commandLine 'git', 'describe', '--always'
            } else {
                commandLine 'git', 'describe', '--always', '--dirty'
            }
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

def getBuildTime = { ->
    return new Date().format("yyyy-MM-dd@HH:mm:ss", TimeZone.getDefault())
}

def getRandomID = { ->
    return UUID.randomUUID().toString()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: "../build.dependencies.gradle"

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'junit:junit:4.13.1'
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 24
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28
        buildConfigField "String", "GIT_COMMIT", '"' + getCommit() + '"'
        buildConfigField "String", "BUILD_TIME", '"' + getBuildTime() + '"'
        buildConfigField "String", "ID", '"' + getRandomID() + '"'
        buildConfigField "String", "SEMVER", '"' + project.version + '"'
        buildConfigField "String", "SDK_VER", '"' + project.sdk_version + '"'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    namespace = 'au.edu.sa.mbhs.studentrobotics.bunyipslib'
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("./docs"))
    moduleVersion.set("v" + project.version + "-master-" + getCommit(true))

    dokkaSourceSets {
        configureEach {
            displayName.set("BunyipsLib")
            jdkVersion.set(8)
            documentedVisibilities.set(
                    [
                            DokkaConfiguration.Visibility.PUBLIC,
                            DokkaConfiguration.Visibility.PROTECTED,
                    ]
            )
            perPackageOption {
                matchingRegex.set("org\\.murraybridgebunyips\\.bunyipslib\\.example\\..*")
                suppress.set(true)
            }
        }
    }
}

tasks.withType(DokkaTask.class).configureEach {
    String dokkaBaseConfiguration = """
    {
      "customAssets": ["${file("docs/custom/logo-icon.svg")}"],
      "separateInheritedMembers": true,
      "footerMessage": "Â© 2024 Lucas Bubner, Murray Bridge High School Student Robotics Club"
    }
    """
    pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": dokkaBaseConfiguration])
}

